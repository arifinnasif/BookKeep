-- PREREQ: sequence__order_id_auto_increment_seq.sql


-- MAKES ORDER WITH AVAILABLE BOOKS IN THE INVENTORY
CREATE OR REPLACE PROCEDURE MAKE_ORDER(ARG_CUSTOMER_ID IN VARCHAR2, ARG_ORDERING_DATE IN DATE) IS
        CNT_CSTMR_IN_CARTS NUMBER;
        O_ID NUMBER;
        U_PRICE NUMBER; -- UNIT PRICE
        IS_SOME_ORDER_VALID NUMBER;
        ORDR_QUANTITY NUMBER;
    BEGIN
        SELECT COUNT(*) INTO CNT_CSTMR_IN_CARTS FROM CARTS WHERE CUSTOMER_ID = ARG_CUSTOMER_ID;
        IF CNT_CSTMR_IN_CARTS <> 0 THEN
            IS_SOME_ORDER_VALID := 0;

            FOR R IN (
                    SELECT ISBN, B.PRICE AS B_PRICE, B.QUANTITY AS B_QUANTITY, CRT.QUANTITY AS CRT_QUANTITY, TOTAL_DCT_PCT AS OFR_TOTAL_DCT_PCT
                    FROM BOOKS B
                    INNER JOIN CARTS CRT USING (ISBN)
                    LEFT OUTER JOIN
                        (
                            SELECT SUM(DISCOUNT_PCT) AS TOTAL_DCT_PCT, ISBN
                            FROM OFFER_BOOK
                            INNER JOIN OFFERS USING(OFFER_ID)
                            WHERE (START_DATE + PERIOD) >= ARG_ORDERING_DATE AND START_DATE <= ARG_ORDERING_DATE
                            GROUP BY ISBN -- UNNECESSARY. AS ISBN IS A PK IN OFFER_BOOK BUT STILL
                        ) OFR USING (ISBN) WHERE CUSTOMER_ID = ARG_CUSTOMER_ID
                )
                LOOP

                ORDR_QUANTITY := LEAST(R.CRT_QUANTITY, R.B_QUANTITY);
                IF ORDR_QUANTITY <> 0 THEN
                    IF IS_SOME_ORDER_VALID = 0 THEN
                        -- THIS BLOCK SHOULD RUN ONCE
                        IS_SOME_ORDER_VALID := 1;
                        O_ID := ORDER_ID_AUTOINCREMENT_SEQ.NEXTVAL;
                        INSERT INTO ORDERS (ORDER_ID, CUSTOMER_ID, ORDERING_DATE) VALUES (O_ID, ARG_CUSTOMER_ID, ARG_ORDERING_DATE);
                    END IF;

                    U_PRICE := R.B_PRICE * (1.0 - NVL(R.OFR_TOTAL_DCT_PCT, 0));

                    INSERT INTO ORDER_BOOK(ORDER_ID, ISBN, UNIT_PRICE, QUANTITY) VALUES (O_ID, R.ISBN, U_PRICE, ORDR_QUANTITY);
                    UPDATE BOOKS B2 SET B2.QUANTITY = B2.QUANTITY - ORDR_QUANTITY WHERE B2.ISBN = R.ISBN;

                    UPDATE CARTS CRT2 SET CRT2.QUANTITY = CRT2.QUANTITY - ORDR_QUANTITY WHERE CRT2.CUSTOMER_ID = ARG_CUSTOMER_ID AND CRT2.ISBN = R.ISBN;
                    DELETE FROM CARTS CRT2 WHERE CRT2.CUSTOMER_ID = ARG_CUSTOMER_ID AND CRT2.ISBN = R.ISBN AND CRT2.QUANTITY <= 0;


                END IF;

                END LOOP;
        END IF;
    END;
/
